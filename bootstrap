#!/bin/zsh

curDir=$(dirname ${(%):-%x})
cd $curDir

git pull origin master

if [ ! -f $HOME/.oh-my-zsh/oh-my-zsh.sh ]; then
  echo "Oh no! Oh My Zsh is not installed! Please visit https://ohmyz.sh for installation instructions. Execute me again when Oh My Zsh is installed."
  exit 1
fi

if [ ! -d $ZSH_CUSTOM/themes/powerlevel10k ]; then
  echo "Snap! Powerlevel10k is not installed! Please follow https://github.com/romkatv/powerlevel10k#oh-my-zsh for installation instructions. Execute me again when Powerlevel10k is installed."
  exit 1
fi

function clone_external_plugins() {
  echo "Cloning external plugins..."

  customPluginthemeDir=$ZSH_CUSTOM/plugins
  externalPlugins=(
    zsh-users/zsh-autosuggestions
    zsh-users/zsh-syntax-highlighting
  )

  for repo in $externalPlugins; do
    if [ ! -d $customPluginthemeDir/${repo:t} ]; then
      git clone https://github.com/${repo} $customPluginthemeDir/${repo:t}
    fi
  done
}

function init() {
  includes=(
    ".zsh"* # leave * outside the quotes to enable the wildcard
    ".p10k.zsh"
    ".aliases"
    ".gitconfig"
    ".git"*".txt"
    ".tmux.conf"
  )
  # following folders are not symlinked because they might contain custom user's executables
  exception_folders=(
    ".bin"
    ".config"
  )

  for include in $includes; do
    ln -v -sf $PWD/$include $HOME
  done

  for exception in $exception_folders; do
    mkdir -v -p $HOME/$exception
    ln -v -sf $PWD/$exception/** $HOME/$exception
  done

  source ~/.zshrc

  if [ $SHELL != '/bin/zsh' ]; then
    chsh -s $(which zsh) # make zsh your default shell
  fi
}

clone_external_plugins

if [[ $1 == "--force" ]] || [[ $1 == "-f" ]]; then
  init
else
  # -q for yes/no answers
  if read -q '?This may overwrite existing files in your home directory. Are you sure? (y/n)'; then
    echo ""
    init
  fi
fi
